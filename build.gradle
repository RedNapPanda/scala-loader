plugins {
    id 'java' // or 'groovy' Must be explicitly applied
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

import org.apache.tools.ant.filters.ReplaceTokens

group 'io.not2excel'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

ext.author = 'not2excel'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

shadowJar {
    baseName = project.name
    classifier = ''
    dependencies {
        include(dependency('org.scala-lang:scala-library:.*'))
        include(dependency('org.scala-lang:scala-reflect:.*'))
        include(dependency('org.json:json:.*'))
        include(dependency('org.clapper:grizzled-slf4j_2.11:.*'))
    }
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifactId = project.name
        }
    }
}

processResources {
    filter { line -> line.replace('${', '@').replace('}', '@') }
    filter ReplaceTokens, tokens: [
            "plugin.name"     : 'Scala-Loader',
            "plugin.version"  : project.version,
            "plugin.mainClass": 'io.not2excel.scala.ScalaLoader',
            "plugin.author"   : author,
            "plugin.depends"  : '[Scala-Loader]'
    ]
}

dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: '2.11.6'
    compile group: 'org.scala-lang', name: 'scala-reflect', version: '2.11.6'
    compile group: 'org.json', name: 'json', version: '20141113'
    compile group: 'org.clapper', name: 'grizzled-slf4j_2.11', version: '1.0.2'

    compile group: 'org.projectlombok', name: 'lombok', version: '1.14.8'
    compile group: 'org.spigotmc', name: 'spigot-api', version: '1.8.7-R0.1-SNAPSHOT'
    compile group: 'org.spigotmc', name: 'spigot', version: '1.8.7-R0.1-SNAPSHOT'
}

task fatJar(dependsOn: ':build', type: Jar) {
    baseName = project.name + '-fatjar'
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        //exclude "something to exclude"
    }
    with jar
}

task migrateBuiltModule(dependsOn: [':fatJar', ':install'], type: Copy) {
    destinationDir = file("./")

    into("./out") {
        from(tasks.withType(Jar).collect())
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

task createPom << {
    pom {
        project {
            groupId 'io.not2excel'
            artifactId 'scala-loader'
            version '1.0-SNAPSHOT'
        }
    }.writeTo("pom.xml")
}
